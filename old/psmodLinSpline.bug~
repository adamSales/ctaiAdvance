model{
 ## measurement model
 for(sw in 1:nsecWorked){
 # gradNew[sw] ~ dbern(probB[sw])
  grad[sw] ~ dbern(prob[sw])
  prob[sw] <- 1/(1+exp(-linPred[sw]))
  linPred[sw] <- alphaU+studEff[studentM[sw]]+secEff[section[sw]]
 }
 ## pretest measurement error model
 for(i in 1:nstuobs){
  xirt[i] ~ dnorm(theta[sidX[i]], precme[i])
 }
 for(i in 1:nstud){
  ## Pretest
  u[i]  ~ dnorm(0, precstu[vargroup[i]])
  theta[i] <- classEffX[cid[i]] +  betaX%*%X[i,]+ u[i]

  ### Usage
  studEff[i] ~ dnorm(muU[i],tauU)
  muU[i] <- bxU*theta[i]+inprod(zx[i,],uxu)+X[i,]%*%betaU+classEffU[cid[i]]

  ### Outcome
  Ynew[i] ~ dnorm(muY[i],tauY[Z[i]+1])
  Y[i] ~ dnorm(muY[i],tauY[Z[i]+1])
  muY[i] <- bxY*theta[i]+inprod(zx[i,],uxy)+X[i,]%*%betaY + useEff[i]+trtEff[i]*Z[i]+classEffY[cid[i]]+pairEffect[pair[i]]

  useEff[i] <- bu[1]+bu[2]*studEff[i]+inprod(zz[i,],uu)
  trtEff[i] <- bt[1]+bt[2]*studEff[i]+inprod(zz[i,],ut)

 }
 for(l in 1:2){
  bu[l]~dnorm(0,1/3)
  bt[l]~dnorm(0,1/3)
 }
 bxY~dnorm(0,1/3)
 bxU~dnorm(0,1/3)
 for(k in 1:nknots){
  uu[k]~dnorm(0,tauuu)
  ut[k]~dnorm(0,tauut)
  uxy[k]~dnorm(0,tauuxy)
  uxu[k]~dnorm(0,tauuxu)
 }
 for(cls in 1:nclass){
  classEffU[cls]~dnorm(0,tauClsU)
  classEffY[cls]~dnorm(schoolEffY[school[cls]],tauClsU)
  classEffX[cls]~dnorm(0,tauClsX)
 }
 for(scl in 1:nscl){
  schoolEffY[scl]~dnorm(0,tauSclY)
 }

 for(covar in 1:ncovar){
  betaU[covar]~dnorm(0,1/3)
  betaY[covar]~dnorm(0,1/3)
  betaX[covar]~dnorm(0,1/3)
 }

 for(pp in 1:npair){
  pairEffect[pp] ~ dnorm(0,1/3)
 }
 secEff[1] <- 0
 for(sec in 2:nsec){
  secEff[sec]~dnorm(0,1/100)
 }
 alphaU~dnorm(0,1/100)


#### priors for variance components: sigma~uniform
 tauU <- pow(sigU,-2)
 sigU ~ dunif(1/100,10)

 tauY[1] <- pow(sigY[1],-2)
 sigY[1] ~ dunif(1/100,10)
 tauY[2] <- pow(sigY[2],-2)
 sigY[2] ~ dunif(1/100,10)

 tauClsU <- pow(sigClsU,-2)
 sigClsU ~ dunif(1/100,10)

 tauClsY <- pow(sigClsY,-2)
 sigClsY ~ dunif(1/100,10)

 tauClsX <- pow(sigClsX,-2)
 sigClsX ~ dunif(1/100,10)

 tauSclY <- pow(sigSclY,-2)
 sigSclY ~ dunif(1/100,10)

 tauuu <- pow(siguuu,-2)
 siguuu ~ dunif(1/100,2)

 tauut <- pow(siguut,-2)
 siguut ~ dunif(1/100,1)

 tauuxy <- pow(siguuxy,-2)
 siguuxy ~ dunif(1/100,1)

 tauuxu <- pow(siguuxu,-2)
 siguuxu ~ dunif(1/100,1)

 for(i in 1:nvg){
  sdstu[i] ~ dunif(0,1)
  varstu[i] <- sdstu[i]*sdstu[i]
  precstu[i] <- 1/varstu[i]
 }

 for(ii in 1:nstud){
  for(kk in 1:nknots){
   zz[ii,kk]<- (studEff[ii]-knot[kk])*step(studEff[ii]-knot[kk])
  }
  for(kx in 1:nknots){
   zx[ii,kx] <- (theta[ii]-knotx[kx])*step(theta[ii]-knotx[kx])
  }
 }


}